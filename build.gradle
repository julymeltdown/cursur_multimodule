buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.springframework.boot' version '3.2.3' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id 'java'
    id 'groovy'
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    
    sourceCompatibility = '17'
    
    dependencies {
        // Spring Boot
        implementation 'org.springframework.boot:spring-boot-starter'
        
        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        
        // Test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'io.rest-assured:rest-assured:5.3.2'
        testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
        testImplementation 'org.spockframework:spock-spring:2.3-groovy-4.0'
    }
    
    tasks.named('test') {
        useJUnitPlatform()
    }
}

// 각 모듈별 의존성 설정은 각 모듈의 build.gradle에서 정의
project(':api') {
    bootJar { enabled = true }
    jar { enabled = false }
    
    dependencies {
        implementation project(':application')
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
    }
}

project(':application') {
    bootJar { enabled = false }
    jar { enabled = true }
    
    dependencies {
        implementation project(':domain')
    }
}

project(':domain') {
    bootJar { enabled = false }
    jar { enabled = true }
    
    dependencies {
        implementation project(':common')
    }
}

project(':infrastructure') {
    bootJar { enabled = false }
    jar { enabled = true }
    
    dependencies {
        implementation project(':domain')
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.flywaydb:flyway-core'
        runtimeOnly 'com.h2database:h2'
        runtimeOnly 'org.postgresql:postgresql'
    }
}

project(':common') {
    bootJar { enabled = false }
    jar { enabled = true }
}